;
; $VER: DirectoryOpus4 Install script 1.7 (10.01.2002) by Christoph Gutjahr

;******************************************************************************
;*****                               SETUP                                *****
;******************************************************************************


; some useful variables

(set #true 1)
(set #false 0)
(set #is_a_file 1)
(set #is_a_dir 2)
(set #quote "\"")
(set #newline "\n")
(set #nothing "")

; catalog languages

(set #def_lang_english   0)
(set #def_lang_deutsch   1)
(set #def_lang_espanol   2)
(set #def_lang_francais  4)
(set #def_lang_italiano  8)
(set #def_lang_magyar   16)
(set #def_lang_polski   32)
(set #def_lang_suomi    64)

; manual languages

(set #def_doc_english  0)
(set #def_doc_espanol  1)
(set #def_doc_francais 2)

; check if the localization is available

(if (exists "catalogs")
    (set #localavail 1)
)

; set up our delete options

(delopts "AskUser" "OkNoDelete" "Force")

; this procedures deletes a file whose name is stored in #temp_file

(procedure p_deletefile
   (if (= (exists #temp_file) #is_a_file)
       (delete #temp_file)
   )
)

; the procedure used for installing a single language with the name #lang

(procedure p_copytranslation
   (copyfiles (source (tackon "Catalogs" #lang))
              (all) (dest (tackon #cat_dir #lang)) (confirm)
              (prompt #copyfiles) (help #copyhelp) (optional "Fail" "Force")
   )
   (copyfiles (source "S") (pattern (cat "#?" #lang "#?"))  (dest #help_dir)
              (confirm) (prompt #copyfiles) (help #copyhelp) (optional "Fail" "Force")
   )
)


;******************************************************************************
;*****                            LOCALISATION                            *****
;******************************************************************************


; *** English ***

(set #welcome (cat "\nWelcome to the Directory Opus installation!\n\n"
                   "Written by Christoph Gutjahr and Eddie Churchill\n"
                   "© 2001,2002 Directory Opus 4 Research Project\n"
              )
)
(set #exists "\nThere seems to be a Directory Opus already installed in your drawer named:\n\n")
(set #exists2 (cat "\n\nDo you want to backup this version first?\n\n"
                   "\(click on \"Help\" for more information\)"
              )
)
(set #existhelp (cat "\nThe installation routine has determined that there is "
                     "already a copy of Directory Opus installed in the specified "
                     "destination directory.\n\n"
                     "Select \"Yes\" and the existing files will be backuped in "
                     "a directory called \"Backup\".\n\nIf you answer \"No\", they "
                     "will be overwritten.\n\n"
                     "Note: Configuration files, or files not directly related to "
                     "the Directory Opus distribution will never get deleted!"
                )
)
(set #askdir (cat "Where would you like Directory Opus installed ?\n"
                  "\(A drawer named 'DirOpus4' will be created, if necessary\)"
             )
)
(set #copyfiles "Copying files needed by DirOpus.")
(set #copyhelp (cat "\nThis will copy the program and support files needed by DirOpus.\n\n"
                    "You can select which parts of the distribution should be installed.\n\n"
                    "Under normal circumstances, it is recommended to copy all files.\n\n"
               )
)
(set #startup "What do you want to do when your machine starts up ?")
(set #startuphelp (cat "\nThe installation routine can modify the boot process "
                       "of your computer, to start up Directory Opus immediately "
                       "at startup.\n\n"
                       "To achieve this, the program will make an additional entry "
                       "in your User-Startup."
                  )
)
(set #schoice1 "Do not auto-start Directory Opus")
(set #schoice2 "Startup Directory Opus")
(set #schoice3 "Startup Directory Opus iconified")
(set #updateus "Updating S:User-Startup.")
(set #addinstr (cat "\nThese instructions need to be added to your 'S:User-Startup', "
                    "to properly configure your system for the use of Directory Opus 4:\n\n"
               )
)
(set #endtext (cat "\nWell that's it folks, have a fun time with Directory Opus 4.\n\n"
                   "If you are upgrading from an earlier version of Directory Opus, "
                   "please reboot your computer now."
              )
)
(set #abort "You aborted the installation process.")
(set #informassign (cat "\nThe assign \"DOpus:\" exists on your system. "
                        "This assign is not needed anymore, please remove "
                        "the following line from your user-startup once the "
                        "installation is finished:\n\nassign DOpus: \""
                   )
)
(set #remfiles (cat "Do you want to scan your hard disk for outdated "
                    "DOpus files and delete them?\n\n"
                    "(Configuration files found will not be deleted of course)"
               )
)
(set #remhelp (cat "Earlier versions of Directory Opus spread their files over "
                   "various system directories. Some users continued to do so "
                   "with 4.x versions, to avoid the need of creating an assign.\n\n"
                   "None of this is needed anymore, but there may be some DOpus "
                   "files still lurking in some places. Answer \"Yes\" to delete "
                   "DOpus related files from your System directories."
              )
)
(set #backupfiles "Backing up existing version of Directory Opus...")
(set #lang_catalog (cat "Which catalog files do you want to install?\n"
                        "(English language is built-in)"
                   )
)
(set #lang_manual "Which version of the documentation would you like to install ?")
(set #removingold "Removing DOpus files from system directories.")

(set #def_cat_lang #def_lang_english)
(set #def_doc_lang #def_doc_english)

; *** Deutsch ***

(if (= @language "deutsch")
(
(set #welcome (cat "\nWillkommen bei der Directory Opus Installation!\n\n"
                   "Von Christoph Gutjahr und Eddie Churchill\n"
                   "© 2001,2002 Directory Opus 4 Research Project\n"
              )
)
(set #exists "\nIm angegeben Zielverzeichnis:\n\n")
(set #exists2 (cat "\n\nscheint bereits eine Version von Directory Opus installiert "
                   "zu sein. Möchten Sie ein Backup dieser Version erstellen?\n\n"
                   "\(ein Unterverzeichnis \"Backup\" wird erstellt und die "
                   "existierenden Dateien dorthin kopiert\)"
              )
)
(set #existhelp (cat "\nDas Installationsprogramm hat herausgefunden, daß sich "
                     "im angewählten Zielverzeichnis bereits eine Version von "
                     "Directory Opus befindet.\n\n"
                     "Klicken sie auf \"Ja\", um ein Backup dieser Version zu "
                     "erstellen, sie wird dann in das Unterverzeichnis \"Backup\" "
                     "verschoben, welches vom Installationsprogramm erstellt wird.\n\n"
                )
)
(set #existhelp2 (cat "Antworten Sie mit \"Nein\", und die existierenden "
                      "Dateien werden überschrieben.\n\n"
                      "In keinem der Fälle werden durch die Installation "
                      "Konfigurationsdateien oder fremde Dateien überschrieben!"
                 )
)
(set #askdir (cat "Wo möchten Sie Directory Opus 4 installieren ?\n"
                  "\(Ein Verzeichnis namens 'DirOpus4' wird erstellt, falls nötig\)"
             )
)
(set #copyfiles "Kopiere Dateien, die von DirOpus benötigt werden.")
(set #copyhelp (cat "\nJetzt werden das Hauptprogramm und die Zusatzdateien "
                    "von Directory Opus 4 installiert.\n\n"
                    "Sie können festlegen, welche Teile der Distribution "
                    "kopiert werden sollen.\n\nIm Normalfall sollten Sie "
                    "alle Files kopieren.\n\n"
               )
)
(set #startup "Was soll beim Booten Ihres Rechners geschehen ?")
(set #startuphelp (cat "\nDas Installationsprogramm kann den Bootvorgang "
                       "ihres Rechners so modifizieren, daß Directory Opus "
                       "automatisch gestartet wird, wenn Ihr Rechner hochfährt.\n\n"
                       "Dazu macht das Programm einen zusätzlichen Eintrag in der User-Startup."
                  )
)
(set #schoice1 "DirOpus nicht automatisch starten")
(set #schoice2 "DirOpus automatisch starten")
(set #schoice3 "DirOpus iconifiziert starten")
(set #updateus "Datei S:User-Startup wird modifiziert.")
(set #addinstr (cat "\nFolgende Befehle müssen der Datei 'S:User-Startup' "
                    "hinzugefügt werden, damit Ihr System für die Nutzung "
                    "von Directory Opus 4 einwandfrei konfiguriert ist:\n\n"
               )
)
(set #endtext (cat "\nOkay Leute, das war's. Viel Spaß mit DirOpus 4.\n\n"
                   "Wenn Sie bereits eine ältere Version von Directory Opus "
                   "benutzt haben, sollten Sie jetzt ihren Rechner neu starten."
              )
)
(set #abort "Sie haben den Vorgang abgebrochen.")
(set #informassign (cat "Der Assign \"DOpus:\" existiert auf Ihrem Computer. "
                        "Diese Zuweisung wird nicht mehr benötigt, bitte "
                        "entfernen Sie nach Beendigung der Installation "
                        "folgende Zeile aus Ihrer User-Startup Datei:\n\n"
                        "assign DOpus: \""
                   )
)
(set #remfiles (cat "Soll ihre Festplatte nach nicht mehr benötigten "
                    "DOpus-Dateien durchsucht werden und diese Dateien "
                    "gelöscht werden?\n\n"
                    "(Konfigurationsdateien werden auf keinen Fall gelöscht)"
               )
)
(set #remhelp (cat "Frühere Versionen von Directory Opus legten verschiedene "
                   "Programmteile in System-Verzeichnissen ab. Manche Benutzer "
                   "des Programmes versuchten auch, durch das Verschieben "
                   "einzelner Programmteile in Systemverzeichnisse die "
                   "Notwendigkeit zu umgehen, einen Assign zu erstellen.\n\n"
                   "Beides ist nun nicht mehr nötig, deshalb können alle DOpus "
                   "Dateien aus Systemverzeichnissen entfernt werden "
                   "(falls sich dort noch welche befinden sollten)."
              )
)
(set #backupfiles "Existierende Dateien werden im Verzeichnis \"Backup\" gesichert.")
(set #lang_catalog "Welche Sprachen sollen installiert werden ?\n(Englisch ist eingebaut)")
(set #lang_manual "Welche Version des Handbuchs wollen Sie installieren ?")
(set #removingold "Entferne DOpus-Dateien aus Systemverzeichnissen.")

(set #def_cat_lang #def_lang_deutsch)
)
)

; *** Español ***

(if (= @language "español")
(
(set #welcome (cat "\nBienvenido a la instalación de Directory Opus.\n\n"
                   "Escrita por Christoph Gutjahr y Eddie Churchill\n\n"
                   "© 2001,2002 del Proyecto de Investigación Directory Opus 4\n"
              )
)
(set #exists "\nParece haber una copia de Directory Opus ya instalada en su directorio:\n\n")
(set #exists2 (cat "\n\n¿Desea hacer una copia de esa versión antes de continuar?\n\n"
                   "\ (pulse \"Ayuda\" para más información)."
              )
)
(set #existhelp (cat "\nInstaller ha encontrado una copia de Directory Opus "
                     "instalada en el directorio destino indicado.\n\n "
                     "Elija \"Sí\" para que los ficheros que la forman sean "
                     "copiados a un directorio llamado \"Backup\".\n\n"
                     "Si elige \"No\", esos ficheros serán sobreescritos.\n\n"
                     "Nota: NO se borrará ningún fichero de configuración, "
                     "ni ficheros no relacionados directamente con la "
                     "distribución de Directory Opus."
                )
)
(set #askdir (cat "¿Dónde le gustaría instalar Directory Opus?\n"
                  "(Se creará un cajón llamado 'DirOpus4' si es necesario)."
             )
)
(set #copyfiles "Copiando los ficheros necesarios para DirOpus.")
(set #copyhelp (cat "\nEsta operación copiará el programa y los ficheros "
                    "de soporte necesarios para DirOpus.\n\n"
                    "Puede elegir qué partes de la distribución "
                    "le interesa instalar.\n\n"
                    "En circunstancias normales, se recomienda copiar todos los ficheros.\n\n"
               )
)
(set #startup "¿Qué desea hacer cuando se reinicie su ordenador?")
(set #startuphelp (cat "\nInstaller puede modificar el fichero User-Startup "
                       "de su partición de arranque, para lanzar Directory "
                       "Opus inmediatamente al arrancarse el sistema.\n\n"
                       "Para ello, añadirá una línea extra a su fichero User-Startup."
                  )
)
(set #schoice1 "No lanzar automáticamente Directory Opus")
(set #schoice2 "Lanzar automáticamente Directory Opus")
(set #schoice3 "Lanzar Directory Opus en modo iconificado")
(set #updateus "Actualizando S:User-Startup.")
(set #addinstr (cat "\nEstas instrucciones necesitan ser añadidas a su fichero "
                    "'S:User-Startup', para configurar su sistema correctamente "
                    "para el uso de Directory Opus 4:\n\n"
               )
)
(set #endtext (cat "\nÉso es todo, ¡que se divierta con Directory Opus 4!\n\n "
                   "Si está actualizándose desde una versión más antigua de "
                   "Directory Opus, por favor, reinicie ahora su ordenador."
              )
)
(set #abort "Ha interrumpido el proceso de instalación.")
(set #informassign (cat "\nLa asignación \"DOpus:\" está presente en su sistema. "
                        "Esta asignación ya no es necesaria; por favor, elimine "
                        "la siguiente línea de su fichero 'User-Startup' una vez "
                        "concluido el proceso de instalación:\n\n"
                        "assign DOpus: \""
                   )
)
(set #remfiles (cat "¿Desea que Installer explore su disco duro en busca "
                    "de viejos ficheros de DOpus para borrarlos?\n\n"
                    "(Los ficheros de configuración que se encuentren "
                    "NO serán borrados, por supuesto)"
               )
)
(set #remhelp (cat "Anteriores versiones de Directory Opus dispersaban sus "
                   "ficheros por varios directorios del sistema. Algunos "
                   "usuarios continuaron así en las versiones 4.x, para "
                   "evitar la necesidad de crear una asignación.\n\n"
                   "Nada de esto es necesario ahora, pero puede haber "
                   "todavía algunos ficheros de DOpus desperdigados por "
                   "alguna parte. Conteste \"Sí\" para borrar los ficheros "
                   "de DOpus de sus directorios del sistema."
              )
)
(set #backupfiles "Creando copia de seguridad de la versión existente de Directory Opus...")
(set #lang_catalog "¿Qué catálogo desea instalar?\n(Por omisión, DOpus trabaja en inglés)")
(set #lang_manual "¿Qué versión de la documentación le gustaría instalar?")
(set #removingold "Eliminando los ficheros de DOpus de los directorios del sistema...")

(set #def_cat_lang #def_lang_espanol)
(set #def_doc_lang #def_doc_espanol)
)
)

; *** Français ***

(if (= @language "français")
(
(set #welcome (cat "Bienvenue dans l'installation de DirectoryOpus 4 !\n\n"
                   "Écrit par Christoph Gutjahr et Eddie Churchill\n"
                   "© 2001,2002 Directory Opus 4 Research Project\n"
              )
)
(set #exists "\nIl semble qu'il y aie déjà un Directory Opus installé dans le répertoire :\n\n")
(set #exists2 (cat "\n\nVoulez-vous sauvegarder cette version avant d'installer ?\n\n"
                   "\(Cliquez sur « Aide » pour plus d'informations.\)"
              )
)
(set #existhelp (cat "\nUne copie de Directory Opus est déjà installée "
                     "dans le répertoire de destination spécifié.\n\n"
                     "Si vous sélectionnez « Oui », les fichiers présents "
                     "seront sauvegardés dans un répertoire appelé « Backup ».\n\n"
                     "Si vous répondez « Non », ils seront écrasés.\n\n"
                     "Remarque : les fichiers de configuration et les fichiers "
                     "non liés à la distribution de Directory Opus ne seront pas effacés !"
                )
)
(set #askdir (cat "Où voulez-vous installer Directory Opus ?\n"
                  "\(Un dossier appelé  « DirOpus4 » sera créé\)"
             )
)
(set #copyfiles "Copie des fichiers de DirOpus en cours.")
(set #copyhelp (cat "\nLes fichiers nécessaires à DirOpus seront installés.\n\n"
                    "Vous pouvez sélectionner les parties de la distribution "
                    "à installer.\n\n"
                    "En temps normal, il est recommandé de copier tous les fichiers.\n\n"
               )
)
(set #startup "Que voulez-vous faire au démarrage de votre système ?")
(set #startuphelp (cat "\nL'installation peut modifier votre séquence de "
                       "démarrage pour que Directory Opus soit démarré "
                       "automatiquement à chaque démarrage.\n\n"
                       "Pour réaliser ceci, le programme ajoutera quelques "
                       "lignes au fichier s:User-Startup."
                  )
)
(set #schoice1 "Ne pas démarrer automatiquement Directory Opus")
(set #schoice2 "Démarrer Directory Opus")
(set #schoice3 "Démarrer Directory Opus iconifié")
(set #updateus "Mise à jour de S:User-Startup.")
(set #addinstr (cat "\nCes instructions doivent être ajoutées au fichier "
                    "« S:User-Startup » pour configurer votre système pour "
                    "Directory Opus 4 :\n\n"
               )
)
(set #endtext (cat "\nL'installation est terminée, bonne utilisation "
                   "de Directory Opus 4 !\n\n"
                   "Si vous venez d'effectuer une mise à jour de Directory Opus, "
                   "merci de redémarrer votre système."
              )
)
(set #abort "Vous avez interrompu la procédure d'installation.")
(set #informassign (cat "\nL'assignation « DOpus: » existe sur votre système. "
                        "Elle n'est plus utile, merci de supprimer la ligne "
                        "suivante de votre user-startup une fois l'installation terminée :\n\n"
                        "assign DOpus: \""
                   )
)
(set #remfiles (cat "Voulez-vous effacer les anciens fichiers de DOpus sur "
                    "votre disque ?\n\n(Les fichiers de configuration ne seront pas effacés.)"
               )
)
(set #remhelp (cat "Les installations de versions précédentes de Directory "
                   "Opus copiaient des fichiers de la distribution un peu "
                   "partout dans les répertoires de votre disque système. "
                   "Certains utilisateurs ont continué à faire ainsi avec "
                   "les versions 4.x, pour éviter la création d'une assignation.\n\n"
                   "Ceci n'est plus indispensable, mais des fichiers de "
                   "DOpus peuvent toujours se trouver sur votre disque. "
                   "Répondez « Oui » pour effacer les fichiers de DOpus "
                   "dans les répertoires du système."
              )
)
(set #backupfiles "Sauvegarde de la version actuelle de Directory Opus...")
(set #lang_catalog "Quelle langue voulez-vous installer ?\n(Langue Anglaise incluse)")
(set #lang_manual "Quelle version de la documentation voulez-vous installer ?")
(set #removingold "Suppresion des fichiers de DOpus des répertoires du système.")


(set #def_cat_lang #def_lang_francais)
(set #def_doc_lang #def_doc_francais)
)
)

; *** Italiano ***

(if (= @language "italiano")
(
(set #welcome (cat "\nWelcome to the Directory Opus installation!\n\n"
                   "Written by Christoph Gutjahr and Eddie Churchill\n"
                   "© 2001,2002 Directory Opus 4 Research Project\n"
              )
)
(set #exists "\nThere seems to be a Directory Opus already installed in your drawer named:\n\n")
(set #exists2 (cat "\n\nDo you want to backup this version first?\n\n"
                   "\(click on \"Help\" for more information\)"
              )
)
(set #existhelp (cat "\nThe installation routine has determined that there is "
                     "already a copy of Directory Opus installed in the specified "
                     "destination directory.\n\n"
                     "Select \"Yes\" and the existing files will be backuped in "
                     "a directory called \"Backup\".\n\nIf you answer \"No\", they "
                     "will be overwritten.\n\n"
                     "Note: Configuration files, or files not directly related to "
                     "the Directory Opus distribution will never get deleted!"
                )
)
(set #askdir (cat "Where would you like Directory Opus installed ?\n"
                  "\(A drawer named 'DirOpus4' will be created, if necessary\)"
             )
)
(set #copyfiles "Copying files needed by DirOpus.")
(set #copyhelp (cat "\nThis will copy the program and support files needed by DirOpus.\n\n"
                    "You can select which parts of the distribution should be installed.\n\n"
                    "Under normal circumstances, it is recommended to copy all files.\n\n"
               )
)
(set #startup "What do you want to do when your machine starts up ?")
(set #startuphelp (cat "\nThe installation routine can modify the boot process "
                       "of your computer, to start up Directory Opus immediately "
                       "at startup.\n\n"
                       "To achieve this, the program will make an additional entry "
                       "in your User-Startup."
                  )
)
(set #schoice1 "Do not auto-start Directory Opus")
(set #schoice2 "Startup Directory Opus")
(set #schoice3 "Startup Directory Opus iconified")
(set #updateus "Updating S:User-Startup.")
(set #addinstr (cat "\nThese instructions need to be added to your 'S:User-Startup', "
                    "to properly configure your system for the use of Directory Opus 4:\n\n"
               )
)
(set #endtext (cat "\nWell that's it folks, have a fun time with Directory Opus 4.\n\n"
                   "If you are upgrading from an earlier version of Directory Opus, "
                   "please reboot your computer now."
              )
)
(set #abort "You aborted the installation process.")
(set #informassign (cat "\nThe assign \"DOpus:\" exists on your system. "
                        "This assign is not needed anymore, please remove "
                        "the following line from your user-startup once the "
                        "installation is finished:\n\nassign DOpus: \""
                   )
)
(set #remfiles (cat "Do you want to scan your hard disk for outdated "
                    "DOpus files and delete them?\n\n"
                    "(Configuration files found will not be deleted of course)"
               )
)
(set #remhelp (cat "Earlier versions of Directory Opus spread their files over "
                   "various system directories. Some users continued to do so "
                   "with 4.x versions, to avoid the need of creating an assign.\n\n"
                   "None of this is needed anymore, but there may be some DOpus "
                   "files still lurking in some places. Answer \"Yes\" to delete "
                   "DOpus related files from your System directories."
              )
)
(set #backupfiles "Backing up existing version of Directory Opus...")
(set #lang_catalog (cat "Which catalog files do you want to install?\n"
                        "(English language is built-in)"
                   )
)
(set #lang_manual "Which version of the documentation would you like to install ?")
(set #removingold "Removing DOpus files from system directories.")

(set #def_cat_lang #def_lang_italiano)
)
)

; *** Magyar ***

(if (= @language "magyar")
(
(set #welcome (cat "\nKöszöntlek a Directory Opus installálásánál!\n\n"
                   "Írta: Christoph Gutjahr és Eddie Churchill\n"
                   "© 2001,2002 Directory Opus 4 Research Project\n"
              )
)
(set #exists "\nÚgy néz ki, hogy a Directory Opus már installálva van a következõ könyvtárba:\n\n")
(set #exists2 (cat "\n\nEl akarod menteni azt a verziót elõször?\n\n"
                   "\(klikkelj a \"Segítségre\" további információkért\)"
              )
)
(set #existhelp (cat "\nAz installáló rutin megállapítota, hogy a megadott "
                     "könyvtárban van már egy Directory Opus instalálva.\n\n"
                     "Válaszd az \"Igent\" és a meglévõ fájlokról biztonsági "
                     "mentés készül a \"Backup\" könyvtába.\n\nHa a \"Nemet\" "
                     "választod, akkor felüeíródnak.\n\n"
                     "Figyelem: Beállításfájlok, vagy amik nem tartoznak a "
                     "Directory Opus csomaghoz, soha nem törlõdnek!"
                )
)
(set #askdir (cat "Hová szeretnéd installálni a Directory Opust ?\n"
                  "\(Egy 'DirOpus4' nevû könyvtár lesz létrehozva, ha szükséges\)"
             )
)
(set #copyfiles "A DirOpushoz szükséges fájlok másolása.")
(set #copyhelp (cat "\nEz átmásolja a DirOpushoz szükséges fájlokat.\n\n"
                    "Kiválaszthatod, hogy a csomag mely elemei legyenek installálva.\n\n"
                    "Alapesetben célszerû minden fájlt átmásolni.\n\n"
               )
)
(set #startup "Mit akarsz, amikor a gép elindul ?")
(set #startuphelp (cat "\nAz installáló rutin tudja módosítani a számítógéped "
                       "indítását, hogy elindítsa a Directory Opust "
                       "azonnal indulásnál.\n\n"
                       "Hogy ezt béghezvigye, betesz még egy bejegyzést a User-Startupba."
                  )
)
(set #schoice1 "Ne induljon a Directory Opus automatikusan")
(set #schoice2 "Directory Opus indítása")
(set #schoice3 "Directory Opus indítása ikonizálva")
(set #updateus "S:User-Startup frissítése.")
(set #addinstr (cat "\nEzeket az utasításokat kell hozzáadni 'S:User-Startup' "
                    "fájlodhoz,hogy a rendszered rendesen be legyen állítva "
                    "a Directory Opus 4 használatához:\n\n"
               )
)
(set #endtext (cat "\nNos ennyi volt, legyen örömötök Directory Opus 4 használatában.\n\n"
                   "Ha egy korábbi verzióról frissítesz, kérlek indítsd újra most a számítógépedet."
              )
)
(set #abort "Megszakítottad az installációs folyamatot.")
(set #informassign (cat "\nA \"DOpus:\" hozzárendelés már létezik a rendszeredben. "
                        "Erre a hozzárendlésre többé már nincs szükség, kérlek "
                        "távolítsd el a következõ sort a user-startup fájlodból, "
                        "ha az installálás befejezõdött:\n\n"
                        "assign DOpus: \""
                   )
)
(set #remfiles (cat "Át akarod nézni a merevlemezedet régi DOpus fájlok "
                    "után és akarod törölni õket?\n\n"
                    "(A megtalált beállításfájlok természetesen nem törlõdnek)"
               )
)
(set #remhelp (cat "A Directory Opus korábbi verziói több különbözõ "
                   "rendszerkönyvtárba másolták be magukat. Néhány "
                   "felhasználó még ezt teszi a 4.x verziókkal, hogy "
                   "elkerülje a hozzárendelés létrehozását.\n\n"
                   "Már egyikre sincs szükség, de még bújkáhat néhány "
                   "fájlevalahol. Válaszolj \"Igennel\", hogy letöröld "
                   "a DOpushoz kapcsolódó fájlokat a rendszerkönyvtáraidból."
              )
)
(set #backupfiles "A Directory Opus meglévõ verziójának mentése...")
(set #lang_catalog "Milyen catalog fájlokat akarsz installálni?\n(Az angol nyelv beépített)")
(set #lang_manual "A dokumentáció melyik verzióját akarod installálni ?")
(set #removingold "DOpus fájlok eltávolítása a rendszerkönyvtárakból.")

(set #def_cat_lang #def_lang_magyar)
)
)

; *** Polski ***

(if (= @language "polski")
(
(set #welcome (cat "\nWitamy w instalacji programu Directory Opus!\n\n"
                   "Skrypt napisany przez Christoph Gutjahr i Eddie Churchill\n"
                   "© 2001,2002 Directory Opus 4 Research Project\n"
              )
)
(set #nofiles "Ten skrypt musi byê w tym samym katalogu co pozostaîe pliki dystrybucji!\n\n")
(set #exists "\nWyglâda na to, ûe program Directory Opus jest juû zainstalowany w katalogu:\n\n")
(set #exists2 (cat "\n\nCzy chcesz stworzyê kopië bezpieczeïstwa przed "
                   "przystâpieniem do instalacji?\n\n"
                   "\(Wybierz \"Pomoc\" w celu uzyskania bliûszych informacji.\)"
              )
)
(set #existhelp (cat "\nProcedura instalujâca wykryîa, ûe w wybranym katalogu "
                     "masz juû zainstalowanâ kopië programu Directory Opus.\n\n"
                     "Wybierz \"Tak\" w celu stworzenia kopii bezpieczeïstwa "
                     "istniejâcych plików i umieszczenia ich w katalogu \"Backup\".\n\n"
                     "Jeôli wybierzesz \"Nie\", pliki zostanâ zastâpione przez "
                     "nowsze wersje.\n\n"
                     "Informacja: Pliki konfiguracyjne, lub niebezpoôrednio "
                     "zwiâzane z programe Directory Opus nie zostanâ skasowane!"
                )
)
(set #askdir (cat "Gdzie chcesz zainstalowaê program Directory Opus?\n"
                  "\(W razie potrzeby zostanie stworzony katalog nazwany 'DirOpus4'\)"
             )
)
(set #copyfiles "Kopiowanie plików wymaganych przez program Directory Opus.")
(set #copyhelp (cat "\nZostanâ skopiowane pliki programu, oraz "
                    "wspomagajâce jego pracë.\n\n"
                    "Moûesz wybraê które elementy dystrybucji powinny zostaê "
                    "zainstalowane.\n\n"
                    "Normalnie wskazane jest zainstalowanie wszystkich plików.\n\n"
                )
)
(set #startup "Czy chcesz aby Directory Opus byî uruchamiany przy starcie komputera?")
(set #startuphelp (cat "\nProcedura instalacyjna moûe zmieniê sposób uruchamiania "
                       "komputera tak, aby uruchomiê program Directory Opus przy "
                       "uruchamianiu komputera.\n\n"
                       "Aby byîo to moûliwe instalator umieôci dodatkowy wpis "
                       "w pliku \"S:User-Startup\"."
                  )
)
(set #schoice1 "Nie uruchamiaj programu")
(set #schoice2 "Uruchom program")
(set #schoice3 "Uruchom program zikonifikowany")
(set #updateus "Aktualizujë plik \"S:User-Startup\".")
(set #addinstr (cat "\nTe instrukcje powinny byê dodane do pliku 'S:User-Startup', "
                    "w celu poprawnego skonfigurowania Twojego systemu do "
                    "uruchomienia programu Directory Opus 4:\n\n"
               )
)
(set #endtext (cat "\nTo juû wszystko koledzy, ûyczë Wam miîo spëdzonego "
                   "czasu z programem Directory Opus 4.\n\n"
                   "Jeôli aktualizowaîeô instalacjë programu ze starszej "
                   "wersji, wykonaj teraz restart komputera."
              )
)
(set #abort "Przerwaleô proces instalacji.")
(set #informassign (cat "\nPrzypis \"DOpus:\" istnieje w Twoim systemie. "
                        "Ten przypis nie jest juû wymagany, po zakoïczeniu "
                        "instalacji proszë usunâc nastëpujâcâ linië z "
                        "pliku \"s:user-startup\":\n\n"
                        "\"assign DOpus: \""
                   )
)
(set #remfiles (cat "Czy chcesz przeszukaê dysk w celu zlokalizowania starych "
                    "plików programu DOpus i skasowaê je?\n\n"
                    "(Oczywiôcie znalezione pliki konfiguracyjne nie zostanâ skasowane.)"
               )
)
(set #remhelp (cat "Wczeôniejsze wersje programu umieszczaîy wîasne pliki w "
                   "wielu katalogach systemowych. Niektórzy uûytkownicy nadal "
                   "uûywajâ katalogów systemowych w celu ominiëcia "
                   "tworzenia przypisania.\n\n"
                   "Ûaden z nich nie jest juû wymagany, lecz pewne pliki "
                   "programu DOpus mogâ czaiê sië  w niektórych miejscach. "
                   "Odpowiedz \"Tak\" aby skasowaê pliki poîâczone z programem "
                   "DOpus z Twoich katalogów systemowych."
              )
)
(set #backupfiles "Tworzë kopië istniejâcej wersji programu Directory Opus...")
(set #lang_catalog (cat "Które wersje jëzykowe chcesz zainstalowaê?\n"
                        "(Jëzyk angielski jest wbudowany)"
                   )
)
(set #lang_manual "Którâ wersjë dokumentacji chcesz zainstalowaê?")
(set #removingold "Usuwam pliki programu z katalogów systemowych.")

(set #def_cat_lang #def_lang_polski)
)
)

; *** Suomi ***

(if (= @language "suomi")
(
(set #welcome (cat "\nWelcome to the Directory Opus installation!\n\n"
                   "Written by Christoph Gutjahr and Eddie Churchill\n"
                   "© 2001,2002 Directory Opus 4 Research Project\n"
              )
)
(set #exists "\nThere seems to be a Directory Opus already installed in your drawer named:\n\n")
(set #exists2 (cat "\n\nDo you want to backup this version first?\n\n"
                   "\(click on \"Help\" for more information\)"
              )
)
(set #existhelp (cat "\nThe installation routine has determined that there is "
                     "already a copy of Directory Opus installed in the specified "
                     "destination directory.\n\n"
                     "Select \"Yes\" and the existing files will be backuped in "
                     "a directory called \"Backup\".\n\nIf you answer \"No\", they "
                     "will be overwritten.\n\n"
                     "Note: Configuration files, or files not directly related to "
                     "the Directory Opus distribution will never get deleted!"
                )
)
(set #askdir (cat "Where would you like Directory Opus installed ?\n"
                  "\(A drawer named 'DirOpus4' will be created, if necessary\)"
             )
)
(set #copyfiles "Copying files needed by DirOpus.")
(set #copyhelp (cat "\nThis will copy the program and support files needed by DirOpus.\n\n"
                    "You can select which parts of the distribution should be installed.\n\n"
                    "Under normal circumstances, it is recommended to copy all files.\n\n"
               )
)
(set #startup "What do you want to do when your machine starts up ?")
(set #startuphelp (cat "\nThe installation routine can modify the boot process "
                       "of your computer, to start up Directory Opus immediately "
                       "at startup.\n\n"
                       "To achieve this, the program will make an additional entry "
                       "in your User-Startup."
                  )
)
(set #schoice1 "Do not auto-start Directory Opus")
(set #schoice2 "Startup Directory Opus")
(set #schoice3 "Startup Directory Opus iconified")
(set #updateus "Updating S:User-Startup.")
(set #addinstr (cat "\nThese instructions need to be added to your 'S:User-Startup', "
                    "to properly configure your system for the use of Directory Opus 4:\n\n"
               )
)
(set #endtext (cat "\nWell that's it folks, have a fun time with Directory Opus 4.\n\n"
                   "If you are upgrading from an earlier version of Directory Opus, "
                   "please reboot your computer now."
              )
)
(set #abort "You aborted the installation process.")
(set #informassign (cat "\nThe assign \"DOpus:\" exists on your system. "
                        "This assign is not needed anymore, please remove "
                        "the following line from your user-startup once the "
                        "installation is finished:\n\nassign DOpus: \""
                   )
)
(set #remfiles (cat "Do you want to scan your hard disk for outdated "
                    "DOpus files and delete them?\n\n"
                    "(Configuration files found will not be deleted of course)"
               )
)
(set #remhelp (cat "Earlier versions of Directory Opus spread their files over "
                   "various system directories. Some users continued to do so "
                   "with 4.x versions, to avoid the need of creating an assign.\n\n"
                   "None of this is needed anymore, but there may be some DOpus "
                   "files still lurking in some places. Answer \"Yes\" to delete "
                   "DOpus related files from your System directories."
              )
)
(set #backupfiles "Backing up existing version of Directory Opus...")
(set #lang_catalog (cat "Which catalog files do you want to install?\n"
                        "(English language is built-in)"
                   )
)
(set #lang_manual "Which version of the documentation would you like to install ?")
(set #removingold "Removing DOpus files from system directories.")

(set #def_cat_lang #def_lang_suomi)
)
)


;******************************************************************************
;*****     Now figure out the initial defaults for a bunch of things      *****
;******************************************************************************


; introduce ourselfs to the viewers

(welcome #welcome)

; set default destination directory, check for the assign

(set @default-dest "SYS:")
(set #DOpus_Dir (getassign "DOpus"))

; tell the user to remove the assign if it exists

(if (<> #DOpus_Dir #nothing)
   (
      (set #oldlevel @user-level)
      (user 2)
      (message #informassign #DOpus_Dir #quote)
      (set @default-dest #DOpus_Dir)
      (user #oldlevel)
   )
)

; ask for the destination directory, append "DirOpus4" if neccessary,
; else ask if old version should be backuped

(set #DOpus_Dir (askdir (prompt #askdir) (help @askdir-help) (default @default-dest) (newpath)
                )
)
(if (<> (exists (tackon #DOpus_Dir "DirectoryOpus")) #is_a_file)
   (
   (set #DOpus_Dir (tackon #DOpus_Dir "DirOpus4"))
   )
)
(if (= (exists (tackon #DOpus_Dir "DirectoryOpus")) #is_a_file)
   (
      (set #backup_old (askbool (prompt #exists #quote #DOpus_Dir #quote #exists2)
                                (help #existhelp #existhelp2)
                                (default 0)
                       )
      )
   )
)

; Shall we clean DOpus files from System directories ?

(set #remove_old (askbool (prompt #remfiles) (help #remhelp) (default 1)))

; the following only if localization is available

(if (= #localavail 1)
   (

      ; Ask the user which language catalogs should be installed

      (set #catalog_lang
         (askoptions
            (prompt  #lang_catalog)
            (help @askoptions-help)
            (choices "Deutsch  (v4.17)"
                     "Español  (v4.17)"
                     "Français (v4.17)"
                     "Italiano (v4.16)"
                     "Magyar   (v4.17)"
                     "Polski   (v4.17)"
                     "Suomi    (v4.16)"
            )
            (default #def_cat_lang)
         )
      )

      ; Ask the user which documentation should be installed

      (set #docs_lang
         (askchoice
            (prompt  #lang_manual)
            (help    @askchoice-help)
            (choices "English" "Español" "Français")
            (default #def_doc_lang)
         )
      )
   )

   ; else reset all localization parameters

   (
      (set def_cat_lang 0)
      (set def_doc_lang 0)
   )
)

; ask the user if he wants DirectoryOpus auto-started when he boots

(set #startup_mode (askchoice (prompt #startup) (help #startuphelp)
                              (choices #schoice1 #schoice2 #schoice3) (default 0)
                   )
)

; tell the user of our progress

(complete 10)


;******************************************************************************
;*****                     The fun is about to begin!                     *****
;******************************************************************************


; Backup old version if the user opted to do so

(if (= #backup_old 1)
   (
      (set #backup_dir (tackon #DOpus_Dir "backup"))
      (copyfiles
         (prompt #backupfiles)
         (source #DOpus_Dir)
         (help @copyfiles-help)
         (dest #backup_dir)
         (pattern "#?")
         (infos)
         (confirm)
      )
   )
)

; tell the user of our progress

(complete 20)

; Remove old files from various system directories, if the user opted to do so

(if (= #remove_old 1)
   (
      (working #removingold)
      (set #temp_file "S:DirectoryOpus.HLP")
      (p_deletefile)
      (set #temp_file "S:ConfigOpus.HLP")
      (p_deletefile)
      (set #temp_file "S:DO_Deutsch.HLP")
      (p_deletefile)
      (set #temp_file "S:CO_Deutsch.HLP")
      (p_deletefile)
      (set #temp_file "C:DOpusRT")
      (p_deletefile)
      (set #temp_file "C:DOpus_Print")
      (p_deletefile)
      (set #temp_file "C:DOpus_Print.info")
      (p_deletefile)
      (set #temp_file "C:DOpus_Disk")
      (p_deletefile)
      (set #temp_file "C:DOpus_Icon")
      (p_deletefile)
      (set #temp_file "C:ConfigOpus")
      (p_deletefile)
      (set #temp_file "C:ConfigOpus.info")
      (p_deletefile)
      (set #temp_file "SYS:ConfigOpus")
      (p_deletefile)
      (set #temp_file "SYS:ConfigOpus.info")
      (p_deletefile)
      (set #temp_file "ConfigOpus")
      (p_deletefile)
      (set #temp_file "ConfigOpus.info")
      (p_deletefile)
      (set #temp_file "LIBS:Dopus.Library")
      (p_deletefile)
   )
)

; tell the user of our progress

(complete 30)

; Copy main distribution files

(set #DOpus_Dir
   (copyfiles
      (prompt #copyfiles)
      (help #copyhelp)
      (source "")
      (dest #DOpus_Dir)
      (pattern "(C|Libs|Modules|DirectoryOpus|#?readme#?|#?copying#?)")
      (infos)
      (optional "Fail" "Force")
      (confirm)
   )
)
(copyfiles
   (prompt #copyfiles)
   (help #copyhelp)
   (source "S")
   (pattern "#?Opus.HLP")
   (dest (tackon #DOpus_Dir "s"))
   (optional "Fail" "Force")
   (confirm)
)

; tell the user of our progress

(complete 80)

; Copy language files

(if (> #catalog_lang 0)
   (
      (set #cat_dir (tackon #DOpus_Dir "catalogs"))
      (if (not (exists #cat_dir))  (makedir #cat_dir))
      (set #help_dir (tackon #DOpus_Dir "s"))
      (if (not (exists #help_dir))  (makedir #help_dir))
      (copyfiles (prompt #copyfiles) (help #copyhelp) (source "Catalogs") (pattern "#?.cd") (dest #cat_dir) (optional "Fail" "Force") (confirm))
      (if (bitand #catalog_lang #def_lang_deutsch)
         (
            (set #lang "deutsch")
            (p_copytranslation)
         )
      )
      (if (bitand #catalog_lang #def_lang_espanol)
         (
            (set #lang "español")
            (p_copytranslation)
         )
      )
      (if (bitand #catalog_lang #def_lang_francais)
         (
            (set #lang "français")
            (p_copytranslation)
         )
      )
      (if (bitand #catalog_lang #def_lang_italiano)
         (
            (set #lang "italiano")
            (p_copytranslation)
         )
      )
      (if (bitand #catalog_lang #def_lang_magyar)
         (
            (set #lang "magyar")
            (p_copytranslation)
         )
      )
      (if (bitand #catalog_lang #def_lang_polski)
         (
            (set #lang "polski")
            (p_copytranslation)
         )
      )
      (if (bitand #catalog_lang #def_lang_suomi)
         (
            (set #lang "suomi")
            (p_copytranslation)
         )
      )
   )
)

; tell the user of our progress

(complete 90)

; Copy the manual and other documentation stuff

(set #doc_dir (tackon #DOpus_Dir "Docs"))
(if (not (exists #doc_dir))
    (makedir #doc_dir (infos))
)
(if (= #docs_lang #def_doc_Espanol)
   (
      (copyfiles (source "Docs/español") (dest #doc_dir)
                 (confirm) (pattern "#?") (prompt #copyfiles) (help #copyhelp)
                 (infos) (optional "Fail" "Force")
      )
   )
   (
      (copyfiles (source "Docs/english") (dest #doc_dir)
                 (confirm) (pattern "~(#?manual#?)") (prompt #copyfiles) (help #copyhelp)
                 (infos) (optional "Fail" "Force")
      )
      (if (= #docs_lang #def_doc_English)
         (copyfiles (source "Docs/english")  (dest #doc_dir)
                    (confirm) (pattern "#?manual#?") (prompt #copyfiles) (help #copyhelp)
                    (infos) (optional "Fail" "Force")
         )
      )
      (if (= #docs_lang #def_doc_Francais)
         (copyfiles (source "Docs/Français") (dest #doc_dir)
                    (confirm) (pattern "#?") (prompt #copyfiles) (help #copyhelp)
                    (infos) (optional "Fail" "Force")
         )
      )
   )
)

; tell the user of our progress

(complete 95)

; modify user-startup if neccessary

(if (> #startup_mode 0)
   (

      ; startup DirectoryOpus

      (if (= #startup_mode 1)
         (set #user_script (cat #user_script "CD " #DOpus_Dir #newline "DirectoryOpus" #newline))
      )

      ; startup DirectoryOpus in ICONIFIED mode

      (if (= #startup_mode 2)
         (set #user_script (cat #user_script "CD " #DOpus_Dir #newline "DirectoryOpus -i" #newline))
      )

      ; modify S:User-Startup

      (working #updateus)
      (startup "DOpus" (prompt #addinstr #user_script) (help @startup-help) (command #user_script)
      )
   )
)

; tell the user of our progress

(complete 100)

; make sure that default-dir is pointing to the right place

(set @default-dest #DOpus_Dir)


;******************************************************************************
;*****                    END THE WHOLE PROCESS NOW                      ******
;******************************************************************************


; display final message for our viewers and exit

(exit #endtext)
